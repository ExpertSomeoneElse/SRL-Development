{$DEFINE SRL_BANKPIN_INCLUDED}
{$IFNDEF SRL_OSR}
  {$I SRL/osr.simba}
{$ENDIF}

type
  TRSBankPin = record(TRSInterface)
    Entering: Boolean;
  end;

{$i elements/bankpin.elements}

procedure TRSBankPin.SetupAlignment(Mode: ERSClientMode); override;
begin
  inherited;

  case Self.Mode of
    ERSClientMode.FIXED:
      begin
        Self.Alignment.Left := [@InterfaceArea.X1];
        Self.Alignment.Right := [@InterfaceArea.X2];
        Self.Alignment.Top := [@InterfaceArea.Y1, 11];
        Self.Alignment.Bottom := [@InterfaceArea.Y2];
        Self.Alignment.Center.MaxWidth := 488;
        Self.Alignment.Center.MaxHeight := 305;
      end;

    ERSClientMode.RESIZABLE_CLASSIC, ERSClientMode.RESIZABLE_MODERN:
      begin
        Self.Alignment.Left := [@InterfaceArea.X1];
        Self.Alignment.Right := [@InterfaceArea.X2];
        Self.Alignment.Top := [@InterfaceArea.Y1, 10];
        Self.Alignment.Bottom := [@InterfaceArea.Y2];
        Self.Alignment.Center.MaxWidth := 488;
        Self.Alignment.Center.MaxHeight := 305;
      end;
  end;
end;

function TRSBankPin.Enter(Pin: String): Boolean;

  function GetDigitToEnter: Integer;
  begin
    if Self.ElementFindText(ERSBankPinElement.STATE, [$FFFFFF], 'FIRST', RS_FONT_BOLD_12)  then Exit(1);
    if Self.ElementFindText(ERSBankPinElement.STATE, [$FFFFFF], 'SECOND', RS_FONT_BOLD_12) then Exit(2);
    if Self.ElementFindText(ERSBankPinElement.STATE, [$FFFFFF], 'THIRD', RS_FONT_BOLD_12)  then Exit(3);
    if Self.ElementFindText(ERSBankPinElement.STATE, [$FFFFFF], 'FOURTH', RS_FONT_BOLD_12) then Exit(4);
  end;

  function ClickDigit(Digit: Char): Boolean;
  var
    Element: ERSBankPinElement;
  begin
    for 1 to 2 do // Check twice to make sure
      for Element in [ERSBankPinElement.DIGIT_1..ERSBankPinElement.DIGIT_10] do
        if Self.ElementFindText(Element, [$007FFF], Digit, RS_FONT_BOLD_12) then
        begin
          Self.ElementClick(Element);

          Exit(True);
        end;

    // Already hovering?
    for Element in [ERSBankPinElement.DIGIT_1..ERSBankPinElement.DIGIT_10] do
      if Mouse.Position() in Self.ElementBounds(Element) then
      begin
        Self.ElementClick(Element);

        Exit(True);
      end;
  end;

var
  Digit: Int32;
begin
  Self.Entering := True;

  try
    while Self.IsOpen() do
    begin
      Digit := GetDigitToEnter();

      if (Digit > 0) and ClickDigit(Pin[Digit]) then
      begin
        if (Digit = 4) then
        begin
          if Bank.IsOpen(4000) then
            Exit(True);
        end else
          WaitUntil(GetDigitToEnter() > Digit, 100, 2000 + Random(1000));
      end;

      Wait(1000, 2000, wdLeft);
    end;

    Result := Bank.IsOpen();
  finally
    Self.Entering := False;
  end;
end;

function TRSBankPin.IsOpen: Boolean;
begin
  Result := Self.ElementFindText(ERSBankPinElement.EXIT, [$0000FF, $FFFFFF], 'Exit', RS_FONT_QUILL_8);
end;

function TRSBankPin.IsOpen(WaitTime: Int32; Interval: Int32 = -1): Boolean; overload;
begin
  if (Interval = -1) then
    Interval := SRL.TruncatedGauss(50, 1500);

  Result := WaitUntil(Self.IsOpen(), Interval, WaitTime);
end;

procedure TRSBankPin.Draw(Bitmap: TMufasaBitmap); override;
begin
  if (not Self.IsOpen()) then
    Exit;

  inherited;
end;

var
  BankPin: TRSBankPin;

function TRSBank.IsOpen(WaitForItems: Boolean = True): Boolean; override;
begin
  if (not BankPin.Entering) and BankPin.IsOpen() and (not BankPin.Enter(Login.GetPlayerPin())) then
    Self.Fatal('Failed to enter bank pin');

  Result := inherited;
end;

begin
  RegisterInterface(@BankPin.Setup, @BankPin.Draw, @BankPin.SetupAlignment);
end;
