{$DEFINE SRL_XPBAR_INCLUDED}
{$IFNDEF SRL_OSR}
  {$I SRL/osr.simba}
{$ENDIF}

type
  ERSXPBarSize = (SMALL, MEDIUM, LARGE);

  TRSXPBar = record(TRSInterface)
    Cache: record
      ClientWidth, ClientHeight: Integer;
      ClientMode: ERSClientMode;

      Bounds: TBox;
    end;

    Size: ERSXPBarSize;
    Bitmaps: array[ERSXPBarSize] of TMufasaBitmap;
  end;

function TRSXPBar.IsOpen: Boolean;
begin
  Result := Self.Read() > 0;
end;

function TRSXPBar.Open: Boolean;
begin
  if Self.IsOpen() then
    Exit(True);

  Mouse.Click(Minimap.GetXPCircle(), MOUSE_LEFT);

  Result := WaitUntil(Self.IsOpen(), SRL.TruncatedGauss(100, 1000), 3000);
end;

procedure TRSXPBar.Draw(Bitmap: TMufasaBitmap); override;
begin
  if not Self.IsOpen() then
    Exit;

  inherited;
end;

function TRSXPBar.CustomBoundsFinder(ClientMode: ERSClientMode): TBox;

  function Find(Size: ERSXPBarSize; Area: TBox; out Bounds: TBox): Boolean;
  var
    X, Y: Integer;
  begin
    Result := FindBitmapToleranceIn(Self.Bitmaps[Size].GetIndex(), X, Y, Area, 5);

    if Result then
    begin
      Bounds.X1 := X;
      Bounds.Y1 := Y;
      Bounds.X2 := X + Self.Bitmaps[Size].GetWidth()  - 1;
      Bounds.Y2 := Y + Self.Bitmaps[Size].GetHeight() - 1;
    end;
  end;

var
  ClientWidth, ClientHeight: Integer;
  Area: TBox;
  Size: ERSXPBarSize;
begin
  GetClientDimensions(ClientWidth, ClientHeight);
  if (Self.Cache.ClientWidth = ClientWidth) and (Self.Cache.ClientHeight = ClientHeight) and (Self.Cache.ClientMode = ClientMode) then
    Exit(Self.Cache.Bounds);

  // Can only currently be at top of mainscreen in vanilla client
  Area := MainScreen.Bounds;
  Area.Y2 := Area.Y1 + 100;

  for Self.Size in ERSXPBarSize do
    if Find(Self.Size, Area, Result) then
      Break;

  Self.Cache := [ClientWidth, ClientHeight, ClientMode, Result];
end;

procedure TRSXPBar.Setup(Name: String); override;
begin
  inherited;

  Self.Bitmaps[ERSXPBarSize.SMALL]  := SRL.AddManagedBitmap(BitmapFromString(119, 29, 'meJzt2jERgAAQA0G8YIASAxigxL8QOgYDfxTsThRcnXXbV5vfcV42tyfywgyRAyIHRA6IHBA5IHJA5IDIAZEDIgdEDogcEDkgckDkgMgBkQMiB0QOiBwQOSByQOSAyIF3ZJvb5w+xn+wG5pOp+A=='));
  Self.Bitmaps[ERSXPBarSize.MEDIUM] := SRL.AddManagedBitmap(BitmapFromString(129, 29, 'meJzt2jERgDAABMF4iYGUGMAAJf6FJBWDg21u5x1c+3Nds+ndz9vUvgQjQgm4EnAl4ErAlYArAVcCrgRcCbgScCXgSsCVgCsBVwKuBFwJuBJwJeBKwJWAKwFXAq4E3D9BU+Nfyna2Abq6x6g='));
  Self.Bitmaps[ERSXPBarSize.LARGE]  := SRL.AddManagedBitmap(BitmapFromString(140, 29, 'meJzt2qERgDAABEF6SQNIGkgDSPovJI5B4nJid76Dsz/Oa1h+834su7fRQZJGfRr1adSnUZ9GfRr1adSnUZ9GfRr1adSnUZ9GfRr1adSnUZ9GfRr1adSnUZ9GfRr1adSnUd+3kWW3/cZsf7YAmM7oUA=='));

  Self.BoundsFinder.CustomFunction := @Self.CustomBoundsFinder;
end;

function TRSXPBar.Read: Int32;
var
  B: TBox;
begin
  B := Self.Bounds();

  case Self.Size of
    ERSXPBarSize.SMALL:  Result := OCR.RecognizeNumber(B, TOCRColorFilter.Create([RSColors.TEXT_WHITE]), RS_FONT_PLAIN_11);
    ERSXPBarSize.MEDIUM: Result := OCR.RecognizeNumber(B, TOCRColorFilter.Create([RSColors.TEXT_WHITE]), RS_FONT_PLAIN_12);
    ERSXPBarSize.LARGE:  Result := OCR.RecognizeNumber(B, TOCRColorFilter.Create([RSColors.TEXT_WHITE]), RS_FONT_BOLD_12);
  end;
end;

var
  XPBar: TRSXPBar;

begin
  XPBar.Setup('XPBar');
end;
