{$DEFINE SRL_DEPOSITBOX_INCLUDED}
{$IFNDEF SRL_OSR}
  {$I SRL/osr.simba}
{$ENDIF}

const
  DEPOSITBOX_DEPOSIT_ALL  = -1;

type
  TRSDepositBox = type TRSInterface;

  TRSDepositBoxItem = record
    Item: TRSItem;
    Quantity: Int32;
  end;

{$i elements/depositbox.elements}

function TRSDepositBox.GetSlotBoxes: TBoxArray;
begin
  Result := Grid(7, 4, 31, 31, [25, 17], [Self.X1 + 38, Self.Y1 + 42]);
end;

procedure TRSDepositBox.SetupAlignment(Mode: ERSClientMode); override;
begin
  inherited;

  case Self.Mode of
    ERSClientMode.FIXED:
      begin
        Self.Alignment.Left := [@InterfaceArea.X1];
        Self.Alignment.Right := [@InterfaceArea.X2];
        Self.Alignment.Top := [@InterfaceArea.Y1];
        Self.Alignment.Bottom := [@InterfaceArea.Y2];
        Self.Alignment.Center.MaxWidth := 452;
        Self.Alignment.Center.MaxHeight := 276;
      end;

    ERSClientMode.RESIZABLE_CLASSIC, ERSClientMode.RESIZABLE_MODERN:
      begin
        Self.Alignment.Left := [@InterfaceArea.X1];
        Self.Alignment.Right := [@InterfaceArea.X2];
        Self.Alignment.Top := [@InterfaceArea.Y1, -1];
        Self.Alignment.Bottom := [@InterfaceArea.Y2];
        Self.Alignment.Center.MaxWidth := 452;
        Self.Alignment.Center.MaxHeight := 276;
      end;
  end;
end;

function TRSDepositBox.IsOpen: Boolean; overload;
begin
  Result := Self.ElementFindText(ERSDepositBoxElement.QUANTITY_1, [Self.COLOR_TEXT_WHITE, Self.COLOR_TEXT_ORANGE], '1', RS_FONT_PLAIN_12);
end;

function TRSDepositBox.IsOpen(WaitTime: Int32): Boolean; overload;
begin
  Result := WaitUntil(Self.IsOpen(), SRL.TruncatedGauss(50, 1500), WaitTime);
end;

function TRSDepositBox.IsCustomQuantity(Quantity: Int32): Boolean;
begin
  Result := not (Quantity in [1,5,10,DEPOSITBOX_DEPOSIT_ALL]);
end;

function TRSDepositBox.DepositHelper(B: TBox; Amount: Int32; UseQuantityButtons: Boolean): Boolean;
var
  Element: ERSDepositBoxElement;
begin
  if UseQuantityButtons then
  begin
    if Self.IsCustomQuantity(Amount) then
    begin
      if Self.ElementToggle(ERSDepositBoxElement.QUANTITY_X, Self.COLOR_BUTTON_RED, 5, 5) then
      begin
        Mouse.Move(B);

        if MainScreen.IsUpText('Deposit-' + ToString(Amount)) then
        begin
          Mouse.Click(MOUSE_LEFT);

          Result := True;
        end else
          Result := ChooseOption.Select('Deposit-X') and Chat.AnswerQuery('Enter amount', ToString(Amount), Random(2000, 4000));
      end;
    end else
    begin
      case Amount of
        1:  Element := ERSDepositBoxElement.QUANTITY_1;
        5:  Element := ERSDepositBoxElement.QUANTITY_5;
        10: Element := ERSDepositBoxElement.QUANTITY_10;
        else
          Element := ERSDepositBoxElement.QUANTITY_ALL;
      end;

      Result := Self.ElementToggle(Element, Self.COLOR_BUTTON_RED, 5, 5);
      if Result then
        Mouse.Click(B, MOUSE_LEFT);
    end;
  end else
  begin
    Mouse.Move(B);

    if (Amount = BANK_DEPOSIT_ALL) then
      Result := ChooseOption.Select('Deposit-All')
    else
      Result := ChooseOption.Select('Deposit-' + ToString(Amount) + ' ', MOUSE_LEFT, True, False) or
                ChooseOption.Select('Deposit-X') and Chat.AnswerQuery('Enter amount', ToString(Amount), Random(2000, 2500));
  end;
end;

function TRSDepositBox.DepositItem(Item: TRSDepositBoxItem; UseQuantityButtons: Boolean): Boolean;
var
  Box: TBox;
begin
  if Self.FindItem(Item.Item, Box) then
    Result := Self.DepositHelper(Box, Item.Quantity, UseQuantityButtons);
end;

function TRSDepositBox.DepositAll: Boolean;
begin
  if (Inventory.Count() = 0) then
    Exit(True);

  Result := Self.IsOpen();
  if Result then
    Self.ElementClick(ERSDepositBoxElement.DEPOSIT_INVENTORY);
end;

{
function TRSDepositBox.Close(PressEscape: Boolean = False): Boolean; 
begin
  if not Self.IsOpen() then
    Exit(True);

  Result := Self.ClickCloseButton(PressEscape) and WaitUntil(not Self.IsOpen(), SRL.TruncatedGauss(50, 1500), Random(1500, 2000));
end;
}

procedure TRSDepositBox.Draw(Bitmap: TMufasaBitmap); override;
begin
  if not Self.IsOpen() then
    Exit;

  inherited;

  Bitmap.DrawBoxes(GetSlotBoxes(), $00FFFF);
end;

var
  DepositBox: TRSDepositBox;

begin
  DepositBox.Setup('DepositBox', [@DepositBox.SetupAlignment, @DepositBox.Draw]);
  DepositBox.OnGetItemBoxes := @DepositBox.OnGetItemBoxes;
end;
